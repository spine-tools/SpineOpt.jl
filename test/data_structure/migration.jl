#############################################################################
# Copyright (C) 2017 - 2021 Spine project consortium
# Copyright SpineOpt contributors
#
# This file is part of SpineOpt.
#
# SpineOpt is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# SpineOpt is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#############################################################################

@testset "find_version" begin
	url = "sqlite://"
    # With no data
    SpineInterface.close_connection(url)
    SpineInterface.open_connection(url)
	@test SpineOpt.find_version(url) == 1
    SpineInterface.close_connection(url)
    # With settings class only
    SpineInterface.open_connection(url)
	data = Dict(:object_classes => ["settings"])
    SpineInterface.import_data(url; data...)
	@test SpineOpt.find_version(url) == 1
    SpineInterface.close_connection(url)
    # With settings class and integer version parameter value
	data = Dict(:object_classes => ["settings"], :object_parameters => [("settings", "version", 8)])
    SpineInterface.open_connection(url)
    SpineInterface.import_data(url; data...)
	@test SpineOpt.find_version(url) == 8
    SpineInterface.close_connection(url)
    # With settings class and string version parameter value
	data = Dict(:object_classes => ["settings"], :object_parameters => [("settings", "version", "77")])
    SpineInterface.open_connection(url)
    SpineInterface.import_data(url; data...)
	@test SpineOpt.find_version(url) == 77
    SpineInterface.close_connection(url)
    # With settings class and float version parameter value
	data = Dict(:object_classes => ["settings"], :object_parameters => [("settings", "version", 44.0)])
    SpineInterface.open_connection(url)
    SpineInterface.import_data(url; data...)
	@test SpineOpt.find_version(url) == 44
    SpineInterface.close_connection(url)
    # With settings class and invalid version parameter value
	data = Dict(:object_classes => ["settings"], :object_parameters => [("settings", "version", "invalid")])
    SpineInterface.open_connection(url)
    SpineInterface.import_data(url; data...)
	@test_throws ArgumentError SpineOpt.find_version(url)
    SpineInterface.close_connection(url)
end

@testset "run_migrations" begin
	file_path, io = mktemp()
	url = "sqlite:///$file_path"
	SpineOpt.run_migrations(url, 1, 0)
	Y = Module()
	using_spinedb(url, Y)
	template = SpineOpt.template()
	object_class_names = [
		Symbol(x[1]) for x in template["entity_classes"] if isempty(x[2])
	]
	compound_classes = [ # Compound classes get processed into superclass ObjectClasses in SpineInterface
		:unit_flow__investment_group,
		:unit_flow__user_constraint,
		:unit_flow__unit_flow,
	]
	append!(object_class_names, compound_classes)
	relationship_class_names = [
		Symbol(x[1]) for x in template["entity_classes"] if !isempty(x[2])
	]
	autogenerated_subclasses = [ # Compound classes get processed into unambiguously dimensioned subclasses in SpineInterface
		:unit_flow__investment_group__unit__node__investment_group,
		:unit_flow__investment_group__node__unit__investment_group,
		:unit_flow__user_constraint__unit__node__user_constraint,
		:unit_flow__user_constraint__node__unit__user_constraint,
		:unit_flow__unit_flow__unit__node__unit__node,
		:unit_flow__unit_flow__unit__node__node__unit,
		:unit_flow__unit_flow__node__unit__unit__node,
		:unit_flow__unit_flow__node__unit__node__unit,
	]
	setdiff!(relationship_class_names, compound_classes)
	append!(relationship_class_names, autogenerated_subclasses)
	parameter_names = [Symbol(x[2]) for x in template["parameter_definitions"]]
	@test Set([x.name for x in object_classes(Y)]) == Set(object_class_names)
	@test Set([x.name for x in relationship_classes(Y)]) == Set(relationship_class_names)
	@test Set([x.name for x in parameters(Y)]) == Set(parameter_names)
	dummy = Object(:dummy, :settings)
	add_objects!(Y.settings, [dummy])
	@test Y.version(settings=dummy) == SpineOpt.current_version()
end

function _test_rename_unit_constraint_to_user_constraint()
	@testset "rename_unit_constraint_to_user_constraint" begin
	end
end

function _test_move_connection_flow_cost()
	@testset "move_connection_flow_cost" begin
		data = Dict(
			:object_classes => ["connection", "node"],
			:relationship_classes => [
				("connection__from_node", ("connection", "node")), ("connection__to_node", ("connection", "node"))
			],
			:object_parameters => [("connection", "connection_flow_cost")],
			:objects => [
				("connection", "conn_ab"),
				("connection", "conn_bc"),
				("node", "node_a"),
				("node", "node_b"),
				("node", "node_c"),
			],
			:relationships => [
				("connection__from_node", ("conn_ab", "node_a")),
				("connection__from_node", ("conn_bc", "node_b")),
				("connection__to_node", ("conn_ab", "node_b")),
				("connection__to_node", ("conn_bc", "node_c")),
			],
			:object_parameter_values => [
				("connection", "conn_ab", "connection_flow_cost", 99),
				("connection", "conn_bc", "connection_flow_cost", -1)
			]
		)
		@testset "successful" begin
			url = "sqlite://"
			_load_test_data_without_template(url, data)
			@test SpineOpt.move_connection_flow_cost(url, 0) === true
			run_request(url, "call_method", ("commit_session", "move_connection_flow_cost"))
			Y = Module()
			using_spinedb(url, Y)
			@test Y.connection_flow_cost(connection=Y.connection(:conn_ab), node=Y.node(:node_a)) == 99
			@test Y.connection_flow_cost(connection=Y.connection(:conn_bc), node=Y.node(:node_b)) == -1
		end
		@testset "unsuccessful" begin
			url = "sqlite://"
			push!(data[:objects], ("connection", "invalid"))
			push!(data[:object_parameter_values], ("connection", "invalid", "connection_flow_cost", 800))
			_load_test_data_without_template(url, data)
			@test_throws Exception SpineOpt.move_connection_flow_cost(url, 0)
		end
	end
end

function _test_rename_model_types()
	@testset "rename_model_types" begin
		url = "sqlite://"
		data = Dict(
			:object_classes => ["model"],
			:objects => [("model", "op"), ("model", "benders")],
			:object_parameters => [("model", "model_type", "spineopt_other", "model_type_list")],
			:object_parameter_values => [
				("model", "op", "model_type", "spineopt_operations"),
				("model", "benders", "model_type", "spineopt_master")
			],
			:parameter_value_lists => [
				("model_type_list", "spineopt_master"),
				("model_type_list", "spineopt_operations"),
				("model_type_list", "spineopt_other")
			]
		)
		_load_test_data_without_template(url, data)
		@test SpineOpt.rename_model_types(url, 0) === true
		run_request(url, "call_method", ("commit_session", "rename_model_types"))
		Y = Module()
		using_spinedb(url, Y)
		@test Y.model_type(model=Y.model(:op)) == :spineopt_standard
		@test Y.model_type(model=Y.model(:benders)) == :spineopt_benders_master
	end
end

function _test_translate_ramp_parameters()
	@testset "translate_ramp_parameters" begin
		url = "sqlite://"
		to_rm_pnames = (
			"min_startup_ramp",
			"min_shutdown_ramp",
			"min_res_startup_ramp",
			"min_res_shutdown_ramp",
			"max_res_startup_ramp",
			"max_res_shutdown_ramp",
		)
		data = Dict(
			:object_classes => ["unit", "node"],
			:relationship_classes => [["unit__to_node", ["unit", "node"]]],
			:relationship_parameters => [
				[["unit__to_node", x] for x in to_rm_pnames];
				[["unit__to_node", "max_startup_ramp"], ["unit__to_node", "max_shutdown_ramp"]]
			],
			:objects => [["unit", "unit_a"], ["node", "node_b"]],
			:relationships => [["unit__to_node", ["unit_a", "node_b"]]],
			:relationship_parameter_values => [
				["unit__to_node", ["unit_a", "node_b"], "max_startup_ramp", 0.5],
				["unit__to_node", ["unit_a", "node_b"], "max_shutdown_ramp", 0.8],
			],
		)
		_load_test_data_without_template(url, data)
		@test SpineOpt.translate_ramp_parameters(url, 0) === true
		run_request(url, "call_method", ("commit_session", "translate_ramp_parameters"))
		Y = Module()
		using_spinedb(url, Y)
		@test isempty(intersect([x.name for x in parameters(Y)], to_rm_pnames))
		@test Y.ramp_limits_startup(unit=Y.unit(:unit_a), node=Y.node(:node_b)) == 0.5
		@test Y.ramp_limits_shutdown(unit=Y.unit(:unit_a), node=Y.node(:node_b)) == 0.8
	end
end

function _test_remove_model_tb_ss()
	@testset "remove_model_tb_ss" begin
		url = "sqlite://"
		to_rm_ec_names = ("model__temporal_block", "model__stochastic_structure")
		data = Dict(
			:object_classes => ["model", "temporal_block", "stochastic_structure"],
			:relationship_classes => [
				["model__temporal_block", ["model", "temporal_block"]],
				["model__stochastic_structure", ["model", "stochastic_structure"]],
			],
		)
		_load_test_data_without_template(url, data)
		@test SpineOpt.remove_model_tb_ss(url, 0) === true
		run_request(url, "call_method", ("commit_session", "remove_model_tb_ss"))
		Y = Module()
		using_spinedb(url, Y)
		@test isempty(intersect([x.name for x in relationship_classes(Y)], to_rm_ec_names))
		@test all(cn in [x.name for x in object_classes(Y)] for cn in (:model, :temporal_block, :stochastic_structure))
	end
end

function _test_update_investment_variable_type()
	@testset "update_investment_variable_type" begin
		url = "sqlite://"
		data = Dict(
			:object_classes => ["connection", "node"],
			:object_parameters => [
				("connection", "connection_investment_variable_type", "variable_type_integer", "variable_type_list"),
				("node", "storage_investment_variable_type", "variable_type_integer", "variable_type_list"),
			],
			:parameter_value_lists => [
				("variable_type_list", "variable_type_integer"),
				("variable_type_list", "variable_type_continuous"),
				("connection_investment_variable_type_list", "connection_investment_variable_type_continuous"),
				("connection_investment_variable_type_list", "connection_investment_variable_type_integer"),
			],
			:objects => [("connection", "conn"), ("node", "n")],
			:object_parameter_values => [
				("connection", "conn", "connection_investment_variable_type", "variable_type_continuous"),
				("node", "n", "storage_investment_variable_type", "variable_type_continuous")
			],
		)
		_load_test_data_without_template(url, data)
		Y = Module()
		using_spinedb(url, Y)
		@test Y.connection_investment_variable_type(connection=Y.connection(:conn)) == :variable_type_continuous
		@test Y.storage_investment_variable_type(node=Y.node(:n)) == :variable_type_continuous
		@test SpineOpt.update_investment_variable_type(url, 0) === true
		run_request(url, "call_method", ("commit_session", "update_investment_variable_type"))
		using_spinedb(url, Y)
		@test Y.connection_investment_variable_type(
			connection=Y.connection(:conn)
		) == :connection_investment_variable_type_continuous
		@test Y.storage_investment_variable_type(node=Y.node(:n)) == :storage_investment_variable_type_continuous
	end
end

function _test_add_model_algorithm()
	@testset "add_model_algorithm" begin
		url = "sqlite://"
		data = Dict(
			:object_classes => ["model"],
			:object_parameters => [
				("model", "model_type", "spineopt_standard", "model_type_list"),
			],
			:parameter_value_lists => [
				("model_type_list", "spineopt_standard"),
				("model_type_list", "spineopt_benders"),
				("model_type_list", "spineopt_mga"),
			],
			:objects => [("model", "test_model")],
			:object_parameter_values => [
				("model", "test_model", "model_type", "spineopt_mga"),
			],
		)
		_load_test_data_without_template(url, data)
		Y = Module()
		using_spinedb(url, Y)
		@test Y.model_type(model=Y.model(:test_model)) == :spineopt_mga
		@test SpineOpt.add_model_algorithm(url, 0) === true
		run_request(url, "call_method", ("commit_session", "add_model_algorithm"))
		using_spinedb(url, Y)
		@test Y.model_type(model=Y.model(:test_model)) == :spineopt_standard
		@test Y.model_algorithm(model=Y.model(:test_model)) == :mga_algorithm
	end
end

function _test_rename_lifetime_to_tech_lifetime()
	@testset "rename_lifetime_to_tech_lifetime" begin
		url = "sqlite://"
		data = Dict(
			:object_classes => ["connection", "node", "unit"],
			:objects => [("connection", "conn"), ("node", "n"), ("unit", "u")],
			:object_parameters => [
				("connection", "connection_investment_lifetime"),
				("node", "storage_investment_lifetime"),
				("unit", "unit_investment_lifetime")
			],
			:object_parameter_values => [
				("connection", "conn", "connection_investment_lifetime", Dict("type" => "duration", "data" => "1Y")),
				("node", "n", "storage_investment_lifetime", Dict("type" => "duration", "data" => "1Y")),
				("unit", "u", "unit_investment_lifetime", Dict("type" => "duration", "data" => "1Y"))
			]
		)
		_load_test_data_without_template(url, data)
		Y = Module()
		using_spinedb(url, Y)
		@test SpineOpt.rename_lifetime_to_tech_lifetime(url, 0) === true
		run_request(url, "call_method", ("commit_session", "rename_lifetime_to_tech_lifetime"))
		using_spinedb(url, Y)
		@test Y.connection_investment_tech_lifetime(connection=Y.connection(:conn)) == Year(1)
		@test Y.storage_investment_tech_lifetime(node=Y.node(:n)) == Year(1)
		@test Y.unit_investment_tech_lifetime(unit=Y.unit(:u)) == Year(1)		
	end
end

function _test_translate_heatrate_parameters()
	@testset "translate_heatrate_parameters" begin
		url = "sqlite://"
		data = Dict(
			:object_classes => ["node", "unit"],
			:relationship_classes => [
				["unit__node__node", ["unit", "node", "node"]],
			],
			:objects => [("node", "n1"), ("node", "n2"), ("unit", "u")],
			:relationships => [("unit__node__node", ["u", "n1", "n2"])],
			:relationship_parameters => [
				("unit__node__node", "unit_incremental_heat_rate"),
				("unit__node__node", "unit_idle_heat_rate")
			],
			:relationship_parameter_values => [
				("unit__node__node", ["u", "n1", "n2"], "unit_incremental_heat_rate", 10),
				("unit__node__node", ["u", "n1", "n2"], "unit_idle_heat_rate", 200)
			]
		)
		_load_test_data_without_template(url, data)
		Y = Module()
		using_spinedb(url, Y)
		@test SpineOpt.translate_heatrate_parameters(url, 0) === true
		run_request(url, "call_method", ("commit_session", "translate_heatrate_parameters"))
		using_spinedb(url, Y)
		@test Y.fix_ratio_in_out_unit_flow(unit=Y.unit(:u), node1=Y.node(:n1), node2=Y.node(:n2)) == 10
		@test Y.fix_units_on_coefficient_in_out(unit=Y.unit(:u), node1=Y.node(:n1), node2=Y.node(:n2)) == 200	
	end
end

function _test_translate_use_economic_representation__use_milestone_years_setup()
	url_in = "sqlite://"
	data = Dict(
		:object_classes => ["model",],
		:objects => [
			("model", "instance"),
		],
		:parameter_value_lists => [("boolean_value_list", true), ("boolean_value_list", false)],
		:object_parameters => [
			("model", "use_economic_representation", false, "boolean_value_list"),
			("model", "use_milestone_years", false, "boolean_value_list"),
		],
	)
	_load_test_data_without_template(url_in, data)
	url_in
end

function _test_translate_use_economic_representation__use_milestone_years()
	_options = (nothing, false, true)
	cases = collect(Iterators.product(_options, _options))
	
	for (use_economic_representation, use_milestone_years) in cases
		@testset "translate_use_economic_representation__use_milestone_years:
		use_economic_representation = $use_economic_representation,
		use_milestone_years = $use_milestone_years" begin
			url = _test_translate_use_economic_representation__use_milestone_years_setup()		
			object_parameter_values = [
				["model", "instance", "use_economic_representation", use_economic_representation],
				["model", "instance", "use_milestone_years", use_milestone_years]
			]
			SpineInterface.import_data(
				url; 
				object_parameter_values=object_parameter_values,
			)

			Y = Module()
			using_spinedb(url, Y)
			@test SpineOpt.translate_use_economic_representation__use_milestone_years(url, 0) === true
			run_request(
				url, "call_method", ("commit_session", "translate_use_economic_representation__use_milestone_years")
			)

			_check = run_request(
				url,
				"query",
				("list_value_sq", "parameter_value_list_sq"),
			)
			@show collect(_check)

			old_parameter_names = [:use_economic_representation, :use_milestone_years]
			new_parameter_name = :multiyear_economic_discounting

			using_spinedb(url, Y)
			
			all_parameter_names = [x.name for x in parameters(Y)]
			@test isempty(intersect(all_parameter_names, old_parameter_names))
			@test new_parameter_name in all_parameter_names
			
			if isnothing(use_economic_representation) || !use_economic_representation
				@test isnothing(Y.multiyear_economic_discounting(model=Y.model(:instance)))
			else
				if isnothing(use_milestone_years) || !use_milestone_years
					@test Y.multiyear_economic_discounting(model=Y.model(:instance)) == :consecutive_years
				elseif use_milestone_years == true
					@test Y.multiyear_economic_discounting(model=Y.model(:instance)) == :milestone_years
				end
			end
		end
	end

	# Test the case where the new parameter and is value list is already present, e.g. by loading the latest template
	url_w_template = _test_translate_use_economic_representation__use_milestone_years_setup()
	_load_test_data(url_w_template, Dict())	# incl. loading the latest template
	Y = Module()
	using_spinedb(url_w_template, Y)
	@test SpineOpt.translate_use_economic_representation__use_milestone_years(url_w_template, 0) === true
end

function _test_dummy_migrations_functions()
	url_in = "sqlite://"
	@testset "dummy_migrations_functions" begin
		@test SpineOpt.add_units_out_of_service_and_min_capacity_margin(url_in,0)
		@test SpineOpt.add_stage_output(url_in,0)
		@test SpineOpt.add_node_availability_factor(url_in,0)
		@test SpineOpt.add_node_state_min_factor(url_in,0)
		@test SpineOpt.add_connection_min_factor(url_in,0)
	end
end

function _test_major_upgrade_1()
	url = "sqlite://"
	log_level = 0
	@testset "major_upgrade_1" begin # Non-comprehensive migration test. Samples every sub-function, though.
		original_data = Dict(
			:object_classes => [
				"commodity",
				"connection",
				"investment_group",
				"node",
				"unit",
				"user_constraint"
			],
			:relationship_classes => [
				("unit__commodity", ("unit", "commodity")),
				("unit__from_node", ("unit", "node")),
				("unit__from_node__investment_group", ("unit", "node", "investment_group")),
				("unit__from_node__user_constraint", ("unit", "node", "user_constraint")),
				("unit__to_node", ("unit", "node")),
				("unit__to_node__investment_group", ("unit", "node", "investment_group")),
				("unit__to_node__user_constraint", ("unit", "node", "user_constraint")),
				("unit__node__node", ("unit", "node", "node")),
			],
			:objects => [
				("commodity", "com"),
				("unit", "u"),
				("node", "n"),
				("user_constraint", "uc"),
				("investment_group", "ig"),
			],
			:relationships => [
				("unit__from_node", ("u", "n")),
				("unit__from_node__user_constraint", ("u", "n", "uc")),
				("unit__from_node__investment_group", ("u", "n", "ig")),
				("unit__to_node", ("u", "n")),
				("unit__to_node__user_constraint", ("u", "n", "uc")),
				("unit__node__node", ("u", "n", "n")),
			],
			:object_parameters => [
				("commodity", "commodity_lodf_tolerance"),
				("unit", "fix_units_out_of_service"),
				("unit", "units_unavailable"),
			],
			:object_parameter_values => [
				("commodity", "com", "commodity_lodf_tolerance", 1),
				("unit", "u", "fix_units_out_of_service", 1),
				("unit", "u", "units_unavailable", 2),
			],
			:relationship_parameters => [
				("unit__from_node", "fix_unit_flow"),
				("unit__from_node__user_constraint", "unit_flow_coefficient"),
				("unit__to_node", "fix_unit_flow"),
				("unit__node__node", "fix_ratio_in_out_unit_flow"),
			],
			:relationship_parameter_values => [
				("unit__from_node", ("u", "n"), "fix_unit_flow", 2),
				("unit__to_node", ("u", "n"), "fix_unit_flow", 3),
				("unit__from_node__user_constraint", ("u", "n", "uc"), "unit_flow_coefficient", 4),
				("unit__node__node", ("u", "n", "n"), "fix_ratio_in_out_unit_flow", 5),
				("unit__node__node", ("u", "n", "n"), "fix_ratio_out_in_unit_flow", 6), # Conflicting definition, let's see what happens.
			],
			:parameter_value_lists => [
				("balance_type_list", "balance_type_none"),
				("balance_type_list", "balance_type_node"),
				("balance_type_list", "balance_type_group"),
				("commodity_physics_list", "commodity_physics_lodf"),
				("commodity_physics_list", "commodity_physics_none"),
				("commodity_physics_list", "commodity_physics_ptdf"),
				("db_lp_solver_list", "HiGHS.jl"),
				("db_mip_solver_list", "Gurobi.jl"),
				("unit_online_variable_type_list", "unit_online_variable_type_binary"),
				("unit_online_variable_type_list", "unit_online_variable_type_integer"),
				("unit_online_variable_type_list", "unit_online_variable_type_linear"),
				("unit_online_variable_type_list", "unit_online_variable_type_none"),
				("connection_investment_variable_type_list", "connection_investment_variable_type_continuous"),
				("connection_investment_variable_type_list", "connection_investment_variable_type_integer"),
				("storage_investment_variable_type_list", "storage_investment_variable_type_continuous"),
				("storage_investment_variable_type_list", "storage_investment_variable_type_integer"),
				("unit_investment_variable_type_list", "unit_investment_variable_type_continuous"),
				("unit_investment_variable_type_list", "unit_investment_variable_type_integer"),
			]
		)
		_load_test_data_without_template(url, original_data)
		SpineOpt.major_upgrade_1(url, log_level) # This runs the migration.
		migrated_data = export_data(url) # Export migrated data (required for examining parameter value lists)
		Y = Module() # Empty module for loading DB into.
		using_spinedb(url, Y) # Load DB into the module.
		obj_class_names = getproperty.(object_classes(Y), :name)
		rel_class_names = getproperty.(relationship_classes(Y), :name)
		param_names = getproperty.(parameters(Y), :name)
		# Check that class and parameter renaming works as intended.
		@test !in(:commodity, obj_class_names)
		@test getproperty.(Y.grid(), :name) == [:com] # New class has the entity from the old one.
		@test Y.lodf_tolerance(grid=Y.grid(:com)) == 1 # New class has the parameters from the old one (renamed).
		@test !in(:commodity_lodf_tolerance, param_names) # Old parameter deleted.
		@test Y.out_of_service_count_fix(unit=Y.unit(:u)) == 3 # Combined parameters sum.
		@test !in(:fix_units_out_of_service, param_names) # Old parameter deleted.
		@test !in(:units_unavailable, param_names) # Old parameter deleted.
		# Check that all necessary superclasses are created correctly.
		@test only(Y.unit__to_node()) == (unit=Y.unit(:u), node=Y.node(:n)) # Correct entities
		@test only(Y.node__to_unit()) == (node=Y.node(:n), unit=Y.unit(:u)) # Correct entities
		@test !in(:unit__from_node, rel_class_names) # Old class deleted.
		@test Y.unit_flow() == [ # Correct entities.
			(node=Y.node(:n), unit=Y.unit(:u)),
			(unit=Y.unit(:u), node=Y.node(:n))
		]
		@test Y.flow_limits_fix(node=Y.node(:n), unit=Y.unit(:u)) == 2 # Check parameter value.
		@test Y.flow_limits_fix(unit=Y.unit(:u), node=Y.node(:n)) == 3 # Check parameter value.
		@test !in(:fix_unit_flow, param_names) # Old parameter deleted.
		@test Y.unit_flow__investment_group() == [ # Correct entities
			(node=Y.node(:n), unit=Y.unit(:u), investment_group=Y.investment_group(:ig))
		]
		@test !in(:unit__from_node__investment_group, rel_class_names) # Old class deleted.
		@test !in(:unit__to_node__investment_group, rel_class_names) # Old class deleted.
		@test Y.unit_flow__user_constraint() == [ # Correct entities.
			(node=Y.node(:n), unit=Y.unit(:u), user_constraint=Y.user_constraint(:uc)),
			(unit=Y.unit(:u), node=Y.node(:n), user_constraint=Y.user_constraint(:uc)),
		]
		@test !in(:unit__from_node__user_constraint, rel_class_names) # Old class deleted.
		@test !in(:unit__to_node__user_constraint, rel_class_names) # Old class deleted.
		@test Y.coefficient_for_unit_flow( # Check parameter value.
			node=Y.node(:n), unit=Y.unit(:u), user_constraint=Y.user_constraint(:uc)
		) == 4
		@test !in(:unit_flow_coefficient, param_names) # Old parameter deleted.
		# Check multidimensional class parameters.
		@test Y.constraint_equality_flow_ratio( # Check parameter value.
			node1=Y.node(:n), unit1=Y.unit(:u), unit2=Y.unit(:u), node2=Y.node(:n)
		) == 5 # Only the first definition seems to end up in the db?
		@test !in(:fix_ratio_in_out_unit_flow, param_names) # Old parameter deleted.
		@test !in(:fix_ratio_out_in_unit_flow, param_names) # Old parameter deleted.
		# Check classes to be removed.
		@test !in(:unit__commodity, rel_class_names)
		# Check parameter value lists, which we first need to parse.
		parameter_value_lists = Dict()
		for (list_name, list_value) in migrated_data["parameter_value_lists"]
			list = get!(parameter_value_lists, list_name, [])
			push!(list, parse_db_value(list_value))
		end
		@test Set(parameter_value_lists["grid_physics_list"]) == Set( # Correct list elements.
			["none", "lodf_physics", "ptdf_physics", "voltage_angle_physics", "pressure_physics"]
		)
		@test !in("commodity_physics_list", keys(parameter_value_lists)) # Old list removed.
		@test !in(:has_voltage_angle, param_names) # Converted parameter removed.
		@test !in(:pressure_physics, param_names) # Converted parameter removed.
		@test parameter_value_lists["solver_lp_list"] == ["HiGHS.jl"] # Correct list elements.
		@test !in("db_lp_solver_list", keys(parameter_value_lists)) # Old list removed.
		@test parameter_value_lists["solver_mip_list"] == ["Gurobi.jl"] # Correct list elements.
		@test !in("db_mip_solver_list", keys(parameter_value_lists)) # Old list removed.
		@test Set(parameter_value_lists["balance_type_list"]) == Set(
			["none", "node_balance", "group_balance"] # Correct list elements.
		)
		@test Set(parameter_value_lists["variable_type_list"]) == Set(
			["binary", "integer", "linear", "none"] # Correct list elements.
		)
		@test !in("connection_investment_variable_type_list", keys(parameter_value_lists)) # Old list removed.
		@test !in("storage_investment_variable_type_list", keys(parameter_value_lists)) # Old list removed.
		@test !in("unit_investment_variable_type_list", keys(parameter_value_lists)) # Old list removed.
	end
end

@testset "migration scripts" begin
	_test_rename_unit_constraint_to_user_constraint()
	_test_move_connection_flow_cost()
	_test_rename_model_types()
	_test_translate_ramp_parameters()
	_test_remove_model_tb_ss()
	_test_update_investment_variable_type()
	_test_add_model_algorithm()
	_test_rename_lifetime_to_tech_lifetime()
	_test_translate_heatrate_parameters()
	_test_translate_use_economic_representation__use_milestone_years()
	_test_dummy_migrations_functions()
	_test_major_upgrade_1()
end